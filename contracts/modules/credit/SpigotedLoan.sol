pragma solidity ^0.8.9;

import {LineOfCredit} from "./LineOfCredit.sol";
import {LoanLib} from "../../utils/LoanLib.sol";
import {CreditLib} from "../../utils/CreditLib.sol";
import {SpigotedLoanLib} from "../../utils/SpigotedLoanLib.sol";
import {MutualConsent} from "../../utils/MutualConsent.sol";
import {ISpigot} from "../../interfaces/ISpigot.sol";
import {ISpigotedLoan} from "../../interfaces/ISpigotedLoan.sol";
import {IERC20} from "@openzeppelin/contracts/token/ERC20/IERC20.sol";
import {SafeERC20} from "@openzeppelin/contracts/token/ERC20/utils/SafeERC20.sol";

contract SpigotedLoan is ISpigotedLoan, LineOfCredit {
    using SafeERC20 for IERC20;

    ISpigot public immutable spigot;

    // 0x exchange to trade spigot revenue for credit tokens for
    address public immutable swapTarget;

    // amount of revenue to take from spigot if loan is healthy
    uint8 public immutable defaultRevenueSplit;

    // max revenue to take from spigot if loan is in distress
    uint8 constant MAX_SPLIT = 100;

    // credit tokens we bought from revenue but didn't use to repay loan
    // needed because Revolver might have same token held in contract as being bought/sold
    mapping(address => uint256) private unusedTokens;

    /**
     * @notice - LineofCredit contract with additional functionality for integrating with Spigot and borrower revenue streams to repay loans
     * @param oracle_ - price oracle to use for getting all token values
     * @param arbiter_ - neutral party with some special priviliges on behalf of borrower and lender
     * @param borrower_ - the debitor for all credit positions in this contract
     * @param swapTarget_ - 0x protocol exchange address to send calldata for trades to
     * @param ttl_ - the debitor for all credit positions in this contract
     * @param defaultRevenueSplit_ - the debitor for all credit positions in this contract
     */
    constructor(
        address oracle_,
        address arbiter_,
        address borrower_,
        address spigot_,
        address swapTarget_,
        uint256 ttl_,
        uint8 defaultRevenueSplit_
    ) LineOfCredit(oracle_, arbiter_, borrower_, ttl_) {
        require(defaultRevenueSplit_ <= MAX_SPLIT);

        spigot = ISpigot(spigot_);
        defaultRevenueSplit = defaultRevenueSplit_;
        swapTarget = swapTarget_;
    }

    function _init() internal virtual override(LineOfCredit) returns(LoanLib.STATUS) {
      if(spigot.owner() != address(this)) return LoanLib.STATUS.UNINITIALIZED;
      return LineOfCredit._init();
    }

    function unused(address token) external view returns (uint256) {
        return unusedTokens[token];
    }

    /**

   * @notice - Claims revenue tokens from Spigot attached to borrowers revenue generating tokens
               and sells them via 0x protocol to repay credits
   * @dev    - callable `arbiter` + `borrower`
               bc they are most incentivized to get best price on assets being sold.
   * @notice see _repay() for more details
   * @param claimToken - The revenue token escrowed by Spigot to claim and use to repay credit
   * @param zeroExTradeData - data generated by 0x API to trade `claimToken` against their exchange contract
  */
    function claimAndRepay(address claimToken, bytes calldata zeroExTradeData)
        external
        whileBorrowing
        returns (uint256 repaid)
    {
        bytes32 id = ids[0];
        Credit memory credit = credits[id];

        require(msg.sender == borrower || msg.sender == arbiter);
        credit =  CreditLib.accrue(credit, id, address(interestRate));

        address targetToken = credit.token;

        (uint256 tokensBought, uint256 totalUnused) = _claimAndTrade(
            claimToken,
            targetToken,
            zeroExTradeData
        );

        unusedTokens[claimToken] = totalUnused;

        repaid = tokensBought + unusedTokens[targetToken];
        uint256 debt = credit.interestAccrued + credit.principal;

        // cap payment to debt value
        if (repaid > debt) repaid = debt;
        // update unused amount based on usage
        if (repaid > tokensBought) {
            // using bought + unused to repay loan
            unusedTokens[targetToken] -= repaid - tokensBought;
        } else {
            //  high revenue and bought more than we need
            unusedTokens[targetToken] += tokensBought - repaid;
        }

        credits[id] = _repay(credit, id, repaid);

        emit RevenuePayment(claimToken, repaid);
    }

    /**
     * @notice allows tokens in escrow to be sold immediately but used to pay down credit later
     * @dev ensures first token in repayment queue is being bought
     * @dev    - callable `arbiter` + `borrower`
     * @param claimToken - the token escrowed in spigot to sell in trade
     * @param zeroExTradeData - 0x API data to use in trade to sell `claimToken` for `credits[ids[0]]`
     * returns - amount of credit tokens bought
     */
    function claimAndTrade(address claimToken, bytes calldata zeroExTradeData)
        external
        whileBorrowing
        returns (uint256)
    {
        require(msg.sender == borrower || msg.sender == arbiter);

        address targetToken = credits[ids[0]].token;
        (uint256 tokensBought, uint256 totalUnused) = _claimAndTrade(
          claimToken,
          targetToken,
          zeroExTradeData
        );
        // add bought tokens to unused balance
        unusedTokens[targetToken] += tokensBought;
        unusedTokens[claimToken] += totalUnused;
        return tokensBought;
    }

    function _claimAndTrade(
      address claimToken,
      address targetToken,
      bytes calldata zeroExTradeData
    )
        internal
        whileBorrowing
        returns (uint256 tokensBought, uint256 totalUnused)
    {
      return SpigotedLoanLib.claimAndTrade(
            claimToken,
            targetToken,
            swapTarget,
            address(spigot),
            unusedTokens[claimToken],
            zeroExTradeData
        );
    }

    //  SPIGOT OWNER FUNCTIONS

    /**
     * @notice changes the revenue split between borrower treasury and lan repayment based on loan health
     * @dev    - callable `arbiter` + `borrower`
     * @param revenueContract - spigot to update
     * @return whether or not split was updated
     */
    function updateOwnerSplit(address revenueContract) external returns (bool) {
        (,uint8 split,  ,bytes4 transferFunc) = spigot.getSetting(revenueContract);

        if(transferFunc == bytes4(0)) { revert NoSpigot(); }

        if(loanStatus == LoanLib.STATUS.ACTIVE && split != defaultRevenueSplit) {
            // if loan is healthy set split to default take rate
            return spigot.updateOwnerSplit(revenueContract, defaultRevenueSplit);
        } else if (loanStatus == LoanLib.STATUS.LIQUIDATABLE && split != MAX_SPLIT) {
            // if loan is in distress take all revenue to repay loan
            return spigot.updateOwnerSplit(revenueContract, MAX_SPLIT);
        }

        return false;
    }

    /**
     * @notice - allow Loan to add new revenue streams to reapy credit
     * @dev    - see Spigot.addSpigot()
     * @dev    - callable `arbiter` + `borrower`
     */
    function addSpigot(
        address revenueContract,
        ISpigot.Setting calldata setting
    )
        external
        mutualConsent(arbiter, borrower)
        returns (bool)
    {
        return spigot.addSpigot(revenueContract, setting);
    }

    /**
     * @notice - allow borrower to call functions on their protocol to maintain it and keep earning revenue
     * @dev    - see Spigot.updateWhitelistedFunction()
     * @dev    - callable `arbiter`
     */
    function updateWhitelist(bytes4 func, bool allowed)
        external
        returns (bool)
    {
        require(msg.sender == arbiter);
        return spigot.updateWhitelistedFunction(func, allowed);
    }

    /**

   * @notice -  transfers revenue streams to borrower if repaid or arbiter if liquidatable
             -  doesnt transfer out if loan is unpaid and/or healthy
   * @dev    - callable by anyone 
   * @return - whether or not spigot was released
  */
    function releaseSpigot() external returns (bool) {
        if (loanStatus == LoanLib.STATUS.REPAID) {
           if(!spigot.updateOwner(borrower)) { revert ReleaseSpigotFailed(); }
            return true;
        }

        if (loanStatus == LoanLib.STATUS.LIQUIDATABLE) {
            if(!spigot.updateOwner(arbiter)) { revert ReleaseSpigotFailed(); }
            return true;
        }

        return false;
    }

    /**

   * @notice - sends unused tokens to borrower if repaid or arbiter if liquidatable
             -  doesnt send tokens out if loan is unpaid but healthy
   * @dev    - callable by anyone 
   * @param token - token to take out
  */
    function sweep(address token) external returns (uint256) {
        if (loanStatus == LoanLib.STATUS.REPAID) {
            return _sweep(borrower, token);
        }
        if (loanStatus == LoanLib.STATUS.INSOLVENT) {
            return _sweep(arbiter, token);
        }

        return 0;
    }

    function _sweep(address to, address token) internal returns (uint256 x) {
        x = unusedTokens[token];
        if (token == address(0)) {
            payable(to).transfer(x);
        } else {
            IERC20(token).safeTransfer(to, x);
        }
        delete unusedTokens[token];
    }

    // allow trading in ETH
    receive() external payable {}
}
